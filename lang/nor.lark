// NoR의 문법을 정의합니다.
start: statement+

statement: property_definition_statement
        | io_statement 
        | data_statement
        | create_graph_statement 
        | DRAW_KEYWORD

property_definition_statement: property_keyword linker STRING

data_statement: DATA_KEYWORD linker vector

linker: "은" | "는" | "=" | "<-"

io_statement: load_command 
            | save_command

load_command: LOAD_KEYWORD STRING
save_command: SAVE_KEYWORD STRING

create_graph_statement: CREATE_GRAPH_KEYWORD STRING

property_keyword: SET_TITLE_KEYWORD | X_LABEL_KEYWORD | Y_LABEL_KEYWORD | SET_TYPE_KEYWORD

// 키워드 정의
CREATE_GRAPH_KEYWORD: "그래프생성"

LOAD_KEYWORD: "로드" | "불러오기" | "가져오기" | "load" | "ld" | "open"
SAVE_KEYWORD: "세이브" | "저장하기" | "저장" | "save" | "sv"
DATA_KEYWORD: "데이터"
SET_TITLE_KEYWORD: "제목" | "타이틀" | "title"
SET_LABELS_KEYWORD: "축이름" | "레이블" | "labels" 
X_LABEL_KEYWORD: "x축" | "엑스축" | "xlabel"
Y_LABEL_KEYWORD: "y축" | "와이축" | "ylabel"
SET_TYPE_KEYWORD: "종류" | "타입" | "type" | "유형"
SET_LEGEND_KEYWORD: "범례" | "legend"
POSITION_KEYWORD: "위치" | "pos" | "position" // 범레의 위치. 
SET_COLOR_KEYWORD: "색" | "색상" | "색깔" | "color" | "colour"
SIZE_KEYWORD: "크기" | "size" // 폰트 크기 또는 그래프 크기 등에 사용될 수 있음
SET_SIZE_KEYWORD: "크기설정" | "그래프크기" | "figsize" // 그래프 전체 크기 설정
SET_FONT_KEYWORD: "글꼴" | "폰트" | "font"
SET_RATIO: "비율" | "그래프비율"
SET_THICKNESS: "굵기" | "선굵기"

DRAW_KEYWORD: "그리기" | "draw" | "그려줘"

// 기본 자료형
atom: NUMBER | STRING
element: atom | vector
elements: element ("," element)*

vector : empty_vector | non_empty_vector

empty_vector: "[" "]"
non_empty_vector: "[" elements "]"

STRING: DQ_STRING | SQ_STRING
BOOLEAN: TRUE | FALSE 

TRUE: "true" | "True" | "참"
FALSE: "false" | "False" | "거짓"

// 쌍따옴표 문자열과 따옴표 문자열.
DQ_STRING: /"[^"\\]*(\\.[^"\\]*)*"/
SQ_STRING: /'[^'\\]*(\\.[^'\\]*)*'/

LINE_COMMENT: /\/\/.*/
BLOCK_COMMENT: /\/\*.*?\*\//s

// 공백을 무시
%import common.WS
%import common.SIGNED_NUMBER -> NUMBER
%ignore WS

// 주석을 무시
%ignore LINE_COMMENT
%ignore BLOCK_COMMENT